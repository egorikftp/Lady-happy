apply plugin: "com.github.ben-manes.versions"

buildscript {
    repositories {
        google()
        jcenter()
        mavenCentral()
        maven {
            url 'https://maven.fabric.io/public'
        }
    }
    dependencies {
        classpath Plugins.gradleTools
        classpath Plugins.kotlinGradle
        classpath Plugins.googleServices
        classpath Plugins.fabric
        // classpath Plugins.dexCount
        classpath Plugins.versionPlugin
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        maven { url 'https://jitpack.io' }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

/**
 * Disable Dagger formatting of generated code
 * */
tasks.withType(JavaCompile).all {
    options.compilerArgs += [
            '-Adagger.formatGeneratedSource=disabled'
    ]
}

afterEvaluate {
    def kapt = extensions.findByName('kapt')
    if (kapt != null) {
        kapt.arguments {
            arg("dagger.formatGeneratedSource", "disabled")
        }
    }
}

/**
 * Enable Dagger incremental annotation processing
 * */
gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Adagger.gradle.incremental"
    }
}